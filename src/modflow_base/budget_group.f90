!
! CODE DEVELOPED BY SCOTT E BOYCE 
!                   CONTACT <seboyce@usgs.gov> or <Boyce@engineer.com>
!
MODULE BUDGET_GROUP_INTERFACE!, ONLY: BUDGET_GROUP
  USE CONSTANTS,                        ONLY: BLNK,NL,BLN, TAB,COM,NEG,Z,ONE,TWO,TEN,DZ,UNO,DOS,DIEZ,TRUE,FALSE
  USE ERROR_INTERFACE,                  ONLY: STOP_ERROR, FILE_IO_ERROR
  USE FILE_IO_INTERFACE,                ONLY: READ_TO_DATA
  USE PARSE_WORD_INTERFACE,             ONLY: PARSE_WORD_UP
  USE GENERIC_INPUT_FILE_INSTRUCTION,   ONLY: GENERIC_INPUT_FILE
  USE GENERIC_BLOCK_READER_INSTRUCTION, ONLY: GENERIC_BLOCK_READER
  IMPLICIT NONE
  PRIVATE
  PUBLIC:: BUDGET_GROUP
  !
  TYPE GROUP_INDEX_LOC
            INTEGER,DIMENSION(:), ALLOCATABLE:: IDX
  END TYPE
  !
  TYPE BUDGET_GROUP
      !LOGICAL:: PRINT_BUDGET =FALSE  --NOT CURRENTLY USED
      INTEGER:: IOUT
      INTEGER:: NGRP
      LOGICAL:: BUDGET_GROUPS = FALSE
      CHARACTER(16),         DIMENSION(:), ALLOCATABLE:: GRP   !NOTE THIS IS LEFT JUSTIFICED AND MUST BE MADE RIGHT JUSTIFIED EXTERNALLY
      TYPE(GROUP_INDEX_LOC), DIMENSION(:), ALLOCATABLE:: GID
      INTEGER,               DIMENSION(:), ALLOCATABLE:: DIM
      CHARACTER(:),                        ALLOCATABLE:: PAK
      !
  CONTAINS
  !
  PROCEDURE, PASS(BD)::  INIT    => BUDGET_GROUPS_INITIALIZE !( BL, DEFAULT )
  PROCEDURE, PASS(BD)::  LOAD    => BUDGET_GROUPS_LOAD       !( BL )
  PROCEDURE, PASS(BD)::  RESET   => BUDGET_GROUPS_PREPARE_FOR_GROUP_RELOAD !( [KEEP_ALLOC] )
  GENERIC::              ADD     => BUDGET_GROUPS_ADD_NEXT_ITEM, BUDGET_GROUPS_ADD_GROUP_LIST !( IDX, GROUP ) or (GROUP(:))
  PROCEDURE, PASS(BD)::  INDEX   => BUDGET_GROUPS_GET_NEXT_INDEX!(IGRP, IDX)
  PROCEDURE, PASS(BD)::  GROUPNUM=> BUDGET_GROUPS_GET_GROUP_NUMBER!(GRP)
  PROCEDURE, PASS(BD)::  BUDGET_GROUPS_ADD_NEXT_ITEM
  PROCEDURE, PASS(BD)::  BUDGET_GROUPS_ADD_GROUP_LIST
  PROCEDURE, PASS(BD)::  DESTROY => DEALLOCATE_BUDGET_GROUPS
  FINAL:: FINAL_DEALLOCATE_BUDGET_GROUPS
  END TYPE
  !
  CONTAINS
  !
  SUBROUTINE BUDGET_GROUPS_INITIALIZE( BD, DEFAULT )
    CLASS(BUDGET_GROUP),    INTENT(INOUT):: BD
    CHARACTER(*),           INTENT(IN   ):: DEFAULT
    !
    CALL DEALLOCATE_BUDGET_GROUPS(BD)  !DEALLOCATE IN CASE IT IS ALREADY ALLOCATED  -- BD%BUDGET_GROUPS = FALSE
    !
    BD%NGRP = ONE
    ALLOCATE(BD%DIM(ONE))
    ALLOCATE(BD%GRP(ONE))
             BD%GRP(ONE) = DEFAULT
    !
    ALLOCATE(BD%GID(ONE))
    ALLOCATE(BD%GID(ONE)%IDX(ONE))
    !
    BD%PAK = DEFAULT
    !
  END SUBROUTINE
  !
  SUBROUTINE BUDGET_GROUPS_LOAD( BD,  BL )
    CLASS(BUDGET_GROUP),        INTENT(INOUT):: BD
    TYPE(GENERIC_BLOCK_READER), INTENT(INOUT):: BL
    TYPE(GENERIC_INPUT_FILE):: FL
    INTEGER:: LLOC, ISTART, ISTOP, I
    CHARACTER(5):: ERROR
    !
    IF( BL%NAME == 'BUDGET_GROUP' .OR. BL%NAME == 'BUDGET_GROUPS') THEN
       IF( BL%NLINE>Z ) THEN
           !
           WRITE(BL%IOUT,'(A)') TRIM(ADJUSTL(BD%GRP(ONE)))//' BUDGET_GROUPS BLOCK FOUND. NOW LOADING CUSTOM BUDGET GROUP NAMES.'
           !
           CALL DEALLOCATE_BUDGET_GROUPS(BD)  !DEALLOCATE IN CASE IT IS ALREADY ALLOCATED
           !
           BD%IOUT = BL%IOUT
           BD%BUDGET_GROUPS = TRUE
           ERROR = 'ERROR'
           CALL BL%START()
           IF(  BL%LINE==ERROR ) CALL STOP_ERROR(INFILE=BL%IU, OUTPUT=BL%IOUT,MSG='UNFORTUNATELY UNKNOWN BUDGET_GROUP BLOCK ERROR.'//BLN//'THIS ERROR OCCURED FOR THE PACKAGE THAT HAS THE DEFAULT BUDGET GROUP NAME "'//BD%PAK//'"'//BLN//'PLEASE DOUBLE CHECK BLOCK SET UP.')
           !
           LLOC = ONE
           CALL FL%OPEN(BL%LINE, LLOC, BL%IOUT, BL%IU, NOSTOP=TRUE, REQKEY=TRUE)  !GET LOCATION THAT HOLDS LIST OF FEEDFILE NAMES
           !
           IF (FL%IU.NE.Z) THEN                         !FOUND "EXTERNAL" OR "OPEN/CLOSE".
               CALL BL%INNER(BLNK,FL%IU,BL%IOUT)        !RELOAD LINES --READ TO BOTTOM OF FILE
               CALL BL%START()
           ELSEIF(FL%IU == Z .AND. .NOT. FL%ERROR) THEN !INTERNAL KEYWORD FOUND, MOVE ONE LINE DOWN
               CALL BL%NEXT()
           !ELSE                                        !GROUP NAMES ARE WITHIN BLOCK AND NO INTERNAL KEYWORD -- ASSUMES FIRST GROUP NAME IS ON CURRENT LINE
           !    FL%IU = Z
           END IF
           !
           IF(  BL%LINE==ERROR ) CALL STOP_ERROR(INFILE=BL%IU, OUTPUT=BL%IOUT,MSG='UNFORTUNATELY UNKNOWN BUDGET_GROUP BLOCK ERROR.'//BLN//'THIS ERROR OCCURED FOR THE PACKAGE THAT HAS THE DEFAULT BUDGET GROUP NAME "'//BD%PAK//'"'//BLN//'PLEASE DOUBLE CHECK BLOCK SET UP.')
           !
           BD%NGRP = BL%LIST%LEN() - BL%LIST%GETPOS() + ONE  !RELY ON LIST POSITION AND LENGTH TO GET NUMBER OF GROUP NAMES -- COULD DO BL%NLINE- BL%GETPOS  BUT NEED TO ADD GETPOS
           !
           ALLOCATE(BD%GRP(BD%NGRP))
           ALLOCATE(BD%GID(BD%NGRP))
           ALLOCATE(BD%DIM(BD%NGRP))
           !
           DO I=ONE, BD%NGRP
                      IF(  BL%LINE==ERROR ) CALL STOP_ERROR(INFILE=BL%IU, OUTPUT=BL%IOUT,MSG='UNFORTUNATELY UNKNOWN BUDGET_GROUP BLOCK ERROR.'//BLN//'THIS ERROR OCCURED FOR THE PACKAGE THAT HAS THE DEFAULT BUDGET GROUP NAME "'//BD%PAK//'"'//BLN//'PLEASE DOUBLE CHECK BLOCK SET UP.')
                      LLOC = ONE
                      CALL PARSE_WORD_UP(BL%LINE,LLOC,ISTART,ISTOP)
                      !
                      BD%GRP(I) = BL%LINE(ISTART:ISTOP)
                      !
                      CALL BL%NEXT()
           END DO
           !
           !CALL READ_TO_DATA(LINE,IU,IOUT)
       ELSE
           WRITE(BL%IOUT,'(A)') 'BUDGET_GROUPS BLOCK FOUND, BUT WAS EMPTY. PACKAGE WILL USE THE DEFAULT OneWater BUDGET NAME "'//TRIM(BD%GRP(ONE))//'"'
       END IF
    END IF
    !  BL and FL are automatically destroyed on return.
  END SUBROUTINE
  !
  SUBROUTINE BUDGET_GROUPS_PREPARE_FOR_GROUP_RELOAD( BD,  KEEP_ALLOC)
    CLASS(BUDGET_GROUP), INTENT(INOUT):: BD
    LOGICAL, OPTIONAL:: KEEP_ALLOC
    INTEGER:: I
    LOGICAL:: DEALLOC
    !
    IF(PRESENT(KEEP_ALLOC)) THEN
        DEALLOC = .NOT. KEEP_ALLOC
    ELSE
        DEALLOC = TRUE
    END IF
    !
    IF(.NOT. BD%BUDGET_GROUPS) THEN
                                                  BD%DIM(1)        = Z
                                                  BD%GID(1)%IDX(1) = Z
    ELSEIF ( BD%BUDGET_GROUPS .AND. DEALLOC) THEN
                                                  BD%DIM = Z
                                                  DO I=ONE, UBOUND(BD%GRP,ONE)
                                                                      IF(ALLOCATED(BD%GID(I)%IDX)) DEALLOCATE(BD%GID(I)%IDX)
                                                  END DO
    END IF
    !
  END SUBROUTINE
  !
  SUBROUTINE BUDGET_GROUPS_ADD_NEXT_ITEM( BD, IDX, GROUP, GID )
    CLASS(BUDGET_GROUP),   INTENT(INOUT):: BD
    INTEGER,               INTENT(IN):: IDX
    CHARACTER(*),OPTIONAL, INTENT(IN   ):: GROUP
    INTEGER,     OPTIONAL, INTENT(OUT  ):: GID
    CHARACTER(16):: GRP
    INTEGER:: I, IG
    INTEGER, DIMENSION(:), ALLOCATABLE:: TMP
    !
    IF     (IDX == Z) THEN
        BD%DIM = Z
        IG     = ONE
    ELSEIF (IDX < Z) THEN
        BD%DIM = NEG * IDX
        IG     = ONE
    ELSE
        !
        IF(PRESENT(GROUP)) THEN
            GRP = GROUP
            !GRP = ADJUSTR(GRP)
        ELSE
            GRP = BD%GRP(1)
        END IF
        !
        IF ( BD%BUDGET_GROUPS ) THEN
             IG = Z
             DO CONCURRENT (I=1:BD%NGRP, BD%GRP(I)==GRP)
                 IG = I
             END DO
             IF(IG == Z) CALL STOP_ERROR(OUTPUT=BD%IOUT, MSG='BUDGET_GROUP ERROR: FAILED TO LOCATE BUDGET GROUP WITH NAME "'//GRP//'" WITHIN LIST OF NAMED BUDGET GROUPS'//BLN//'THIS ERROR OCCURED FOR THE PACKAGE THAT HAS THE DEFAULT BUDGET GROUP NAME "'//BD%PAK//'"')
             !
             IF (ALLOCATED(BD%GID(IG)%IDX)) THEN
                                              ALLOCATE( TMP, SOURCE=[BD%GID(IG)%IDX, IDX] )
                                              CALL MOVE_ALLOC(TMP,BD%GID(IG)%IDX)
             ELSE
                                              ALLOCATE( BD%GID(IG)%IDX(1), SOURCE=IDX )
             END IF
             
        ELSE
             IG = ONE
        END IF
        !
        BD%DIM(IG) = BD%DIM(IG) + ONE
        !
    END IF
    !
    IF(PRESENT(GID)) GID = IG
    !
  END SUBROUTINE
  !
  SUBROUTINE BUDGET_GROUPS_ADD_GROUP_LIST( BD, GROUP )
    CLASS(BUDGET_GROUP),                   INTENT(INOUT):: BD
    CHARACTER(*),DIMENSION(:), CONTIGUOUS, INTENT(IN   ):: GROUP
    INTEGER:: I, IG, IDX
    INTEGER, DIMENSION(:), ALLOCATABLE:: TMP
    !
    DO IDX=ONE, UBOUND(GROUP,ONE)
        !
        IF ( BD%BUDGET_GROUPS ) THEN
             IG = Z
             DO CONCURRENT (I=1:BD%NGRP, BD%GRP(I)==GROUP(IDX))
                 IG = I
             END DO
             IF(IG == Z) CALL STOP_ERROR(OUTPUT=BD%IOUT, MSG='BUDGET_GROUP ERROR: FAILED TO LOCATE BUDGET GROUP WITH NAME "'//GROUP(IDX)//'" WITHIN LIST OF NAMED BUDGET GROUPS'//BLN//'THIS ERROR OCCURED FOR THE PACKAGE THAT HAS THE DEFAULT BUDGET GROUP NAME "'//BD%PAK//'"')
             !
             IF (ALLOCATED(BD%GID(IG)%IDX)) THEN
                                              ALLOCATE( TMP, SOURCE=[BD%GID(IG)%IDX, IDX] )
                                              CALL MOVE_ALLOC(TMP,BD%GID(IG)%IDX)
             ELSE
                                              ALLOCATE( BD%GID(IG)%IDX(1), SOURCE=IDX )
             END IF
             
        ELSE
             IG = ONE
        END IF
        !
        BD%DIM(IG) = BD%DIM(IG) + ONE
        !
    END DO
    !
  END SUBROUTINE
  !
  FUNCTION BUDGET_GROUPS_GET_NEXT_INDEX( BD, IGRP, IDX ) RESULT (LOC)
    CLASS(BUDGET_GROUP),   INTENT(INOUT):: BD
    INTEGER,               INTENT(IN   ):: IGRP, IDX
    INTEGER:: LOC
    !
    IF ( BD%BUDGET_GROUPS ) THEN
         LOC = BD%GID(IGRP)%IDX(IDX)
    ELSE
         LOC = IDX
    END IF
    !
  END FUNCTION
  !
  FUNCTION BUDGET_GROUPS_GET_GROUP_NUMBER( BD, GRP ) RESULT (IGRP)
    CLASS(BUDGET_GROUP),   INTENT(IN):: BD
    CHARACTER(16),         INTENT(IN):: GRP
    INTEGER:: IGRP
    INTEGER:: I
    !
    IF ( BD%BUDGET_GROUPS ) THEN
         IGRP = Z
         DO CONCURRENT (I=1:BD%NGRP, BD%GRP(I)==GRP)
             IGRP = I
         END DO
         IF( IGRP == Z) CALL STOP_ERROR(OUTPUT=BD%IOUT, MSG='BUDGET_GROUP ERROR: FAILED TO LOCATE BUDGET GROUP WITH NAME "'//GRP//'" WITHIN LIST OF NAMED BUDGET GROUPS'//BLN//'THIS ERROR OCCURED FOR THE PACKAGE THAT HAS THE DEFAULT BUDGET GROUP NAME "'//BD%PAK//'"')
    ELSE
             IGRP = ONE
    END IF
    !
  END FUNCTION
  !
  PURE ELEMENTAL SUBROUTINE DEALLOCATE_BUDGET_GROUPS(BD)
    CLASS(BUDGET_GROUP),   INTENT(INOUT):: BD
      BD%IOUT=Z
      BD%NGRP=Z
      BD%BUDGET_GROUPS = FALSE
      IF(ALLOCATED(BD%GRP)) DEALLOCATE(BD%GRP)
      IF(ALLOCATED(BD%GID)) DEALLOCATE(BD%GID)
      IF(ALLOCATED(BD%DIM)) DEALLOCATE(BD%DIM)
  END SUBROUTINE
  !
  SUBROUTINE FINAL_DEALLOCATE_BUDGET_GROUPS(BD)
    TYPE(BUDGET_GROUP),   INTENT(INOUT):: BD
      CALL DEALLOCATE_BUDGET_GROUPS(BD)
  END SUBROUTINE
  !
  !!!PURE SUBROUTINE BUDGET_GROUPS_DEALLOCATE(BD)
  !!!  TYPE(BUDGET_GROUP),ALLOCATABLE,INTENT(INOUT):: BD
  !!!  INTEGER:: I
  !!!  !
  !!!  DEALLOCATE(BD, STAT=I)
  !!!  !
  !!!END SUBROUTINE
  !!!!
  !!!PURE SUBROUTINE BUDGET_GROUPS_DEPOINT(BD)
  !!!  TYPE(BUDGET_GROUP),POINTER,INTENT(INOUT):: BD
  !!!  INTEGER:: I
  !!!  !
  !!!  DEALLOCATE(BD, STAT=I)
  !!!  !
  !!!END SUBROUTINE
  !
!  SUBROUTINE BUDGET_GROUPS_PREPARE_FOR_GROUP_RELOAD( BD )
!    CLASS(BUDGET_GROUP), INTENT(INOUT):: BD
!    INTEGER:: I
!    !
!    BD%IDX%DIM = 0
!    IF (BD%BUDGET_GROUPS) THEN
!        DO I=1, UBOUND(BD,1)
!            IF(ALLOCATED(BD%IDX(I))) DEALLOCATE(BD%IDX(I))
!        END DO
!    ELSE
!        BD%IDX(1)%I(1) = 0
!    END IF
!    !
END MODULE
!
!@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
!@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
!@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
!  