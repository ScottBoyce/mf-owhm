!
!#########################################################################################################
!
MODULE OPTIONS_DATA_FMP_MODULE!, ONLY: OPTIONS_DATA, INITIALIZE_OPTIONS_DATA
  !
  USE FMP_DIMENSION_MODULE, ONLY: FMP_DIMENSION
  !
  USE CONSTANTS
  USE ERROR_INTERFACE,                   ONLY: STOP_ERROR, FILE_IO_ERROR, WARNING_MESSAGE
  USE PARSE_WORD_INTERFACE,              ONLY: PARSE_WORD_UP
  USE STRINGS,                           ONLY: GET_NUMBER
  USE NUM2STR_INTERFACE,                 ONLY: NUM2STR
  USE ALLOC_INTERFACE,                   ONLY: ALLOC
  USE GENERIC_BLOCK_READER_INSTRUCTION,  ONLY: GENERIC_BLOCK_READER
  USE WARNING_TYPE_INSTRUCTION,          ONLY: WARNING_TYPE
  !
  IMPLICIT NONE
  PRIVATE
  PUBLIC:: OPTIONS_DATA, INITIALIZE_OPTIONS_DATA
  !
  TYPE OPTIONS_DATA
      LOGICAL:: NOPRINT     = FALSE
      LOGICAL:: WELLFIELD   = FALSE
      LOGICAL:: RECOMP_Q_BD = FALSE
      LOGICAL:: HAS_MNWCLOSE= FALSE
      DOUBLE PRECISION,    DIMENSION(:), ALLOCATABLE:: MNWCLOSE
      !
      CONTAINS
      !
      FINAL:: DEALLOCATE_OPTIONS_FINAL
  END TYPE
  !
  CONTAINS
  !
  SUBROUTINE DEALLOCATE_OPTIONS_FINAL(OPT)
    TYPE(OPTIONS_DATA)::OPT
    CALL DEALLOCATE_OPTIONS(OPT)
  END SUBROUTINE
  !
  PURE ELEMENTAL SUBROUTINE DEALLOCATE_OPTIONS(OPT)
    CLASS(OPTIONS_DATA), INTENT(INOUT)::OPT
    !
    IF(OPT%HAS_MNWCLOSE) DEALLOCATE(OPT%MNWCLOSE)
    OPT%NOPRINT     = FALSE
    OPT%WELLFIELD   = FALSE
    OPT%RECOMP_Q_BD = FALSE
    OPT%HAS_MNWCLOSE= FALSE
  END SUBROUTINE
  !  
  SUBROUTINE INITIALIZE_OPTIONS_DATA( BL, OPT )
    CLASS(GENERIC_BLOCK_READER), INTENT(INOUT):: BL   !DATA BLOCK
    CLASS(OPTIONS_DATA),         INTENT(INOUT):: OPT
    TYPE(WARNING_TYPE):: WARN_MSG
    CHARACTER(5):: ERROR
    INTEGER:: I, LLOC, ISTART, ISTOP
    !
    WRITE(BL%IOUT,'(/A/)') 'OPTIONS BLOCK FOUND AND NOW LOADING OPTIONS'
    !
    OPT%NOPRINT = FALSE
    !
    ERROR='ERROR'
    CALL WARN_MSG%INIT()
    !
    CALL BL%START()
    !
    DO I=ONE, BL%NLINE
      !
      LLOC=ONE
      CALL PARSE_WORD_UP(BL%LINE,LLOC,ISTART,ISTOP)
      !
      SELECT CASE ( BL%LINE(ISTART:ISTOP) )
      CASE ("NOPRINT")
                        WRITE(BL%IOUT,'(A)') '   NOPRINT     OPTION FOUND.'
                        OPT%NOPRINT = TRUE
      CASE ("WELLFIELD")
                        WRITE(BL%IOUT,'(A)') '   WELLFIELD   OPTION FOUND.'
                        OPT%WELLFIELD = TRUE
                        !
      CASE ("RECOMP_Q_BD")
                        WRITE(BL%IOUT,'(A)') '   RECOMP_Q_BD OPTION FOUND.'
                        OPT%RECOMP_Q_BD = TRUE
                        !
      CASE ("MNWCLOSE")
                        WRITE(BL%IOUT,'(A)') '   MNWCLOSE    OPTION FOUND. NOW READING THE NUMBERS QCLOSE, HPCT, RPCT ON SAME LINE.'
                        OPT%HAS_MNWCLOSE = TRUE
                        ALLOCATE(OPT%MNWCLOSE(THREE))
                        CALL GET_NUMBER(BL%LINE,LLOC,ISTART,ISTOP,BL%IOUT,BL%IU,OPT%MNWCLOSE(ONE),  MSG='FMP OPTION BLOCK ERROR; OPTION MNWCLOSE FAILED TO LOAD QCLOSE.')
                        CALL GET_NUMBER(BL%LINE,LLOC,ISTART,ISTOP,BL%IOUT,BL%IU,OPT%MNWCLOSE(TWO),  MSG='FMP OPTION BLOCK ERROR; OPTION MNWCLOSE FAILED TO LOAD HPCT.')
                        CALL GET_NUMBER(BL%LINE,LLOC,ISTART,ISTOP,BL%IOUT,BL%IU,OPT%MNWCLOSE(THREE),MSG='FMP OPTION BLOCK ERROR; OPTION MNWCLOSE FAILED TO LOAD RPCT.')
                        !
      CASE DEFAULT
                        CALL WARN_MSG%ADD('FOUND UNKNOWN KEYWORD "'//BL%LINE(ISTART:ISTOP)//'" ***IT WILL BE IGNORED***'//BLN)
                        
      END SELECT
      !
      CALL BL%NEXT()
      !
    END DO
    !
    CALL WARN_MSG%CHECK(HED='FMP OPTIONS BLOCK'//NL,INFILE=BL%IU,OUTPUT=BL%IOUT,INLINE=TRUE,CMD_PRINT=TRUE,TAIL=NL)
    !
  END SUBROUTINE
  !
END MODULE
!
!#########################################################################################################
!
