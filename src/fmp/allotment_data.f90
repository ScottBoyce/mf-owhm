!
!#########################################################################################################
!
MODULE ALLOTMENT_DATA_FMP_MODULE
  !
  USE FMP_DIMENSION_MODULE, ONLY: FMP_DIMENSION
  !
  USE CONSTANTS
  USE ULOAD_AND_SFAC_INTERFACE
  USE ERROR_INTERFACE,                   ONLY: STOP_ERROR, WARNING_MESSAGE, FILE_IO_ERROR
  USE PARSE_WORD_INTERFACE,              ONLY: PARSE_WORD, PARSE_WORD_UP
  USE NUM2STR_INTERFACE,                 ONLY: NUM2STR
  USE ALLOC_INTERFACE,                   ONLY: ALLOC
  USE GENERIC_BLOCK_READER_INSTRUCTION,  ONLY: GENERIC_BLOCK_READER
  USE LIST_ARRAY_INPUT_INTERFACE,        ONLY: LIST_ARRAY_INPUT, LIST_ARRAY_INPUT_INT
  USE WARNING_TYPE_INSTRUCTION,          ONLY: WARNING_TYPE
  IMPLICIT NONE
  PRIVATE
  PUBLIC:: ALLOTMENT_DATA, INITIALIZE_ALLOTMENT_DATA
  !
  TYPE ALLOTMENT_DATA
      INTEGER:: IOUT=Z, LOUT=Z, NFARM=Z
      INTEGER:: SW_ALLOTMENT_TYPE= Z
      INTEGER:: GW_ALLOTMENT_TYPE= Z
      LOGICAL:: HAS_GW_ALLOTMENT = FALSE
      LOGICAL:: HAS_SW_ALLOTMENT = FALSE
      LOGICAL:: TFR_READ   = FALSE
      DOUBLE PRECISION,    DIMENSION(:),ALLOCATABLE:: GW_ALLOTMENT !VOLUME FOR SP
      DOUBLE PRECISION,    DIMENSION(:),ALLOCATABLE:: GW_RATE_LIM  !VOLUME RATE LIM BASED ON GW_ALLOTMENT FOR TS
      DOUBLE PRECISION,    DIMENSION(:),ALLOCATABLE:: SW_ALLOTMENT !VOLUME FOR SP
      DOUBLE PRECISION,    DIMENSION(:),ALLOCATABLE:: SW_RATE_LIM  !VOLUME RATE LIM BASED ON SW_ALLOTMENT FOR TS
      !
      TYPE(LIST_ARRAY_INPUT):: GW_ALLOTMENT_TFR
      TYPE(LIST_ARRAY_INPUT):: SW_ALLOTMENT_TFR
      !
      CONTAINS
      !
      PROCEDURE, PASS(ALOT):: NEXT     => SETUP_NEXT_STRESS_PERIOD
      FINAL:: DEALLOCATE_ALLOTMENT_FINAL
  END TYPE
  !
  CONTAINS
  !
  SUBROUTINE DEALLOCATE_ALLOTMENT_FINAL(ALOT)
    TYPE(ALLOTMENT_DATA)::ALOT
    CALL DEALLOCATE_ALLOTMENT(ALOT)
  END SUBROUTINE
  !
  PURE ELEMENTAL SUBROUTINE DEALLOCATE_ALLOTMENT(ALOT)
    CLASS(ALLOTMENT_DATA), INTENT(INOUT)::ALOT
    !
    IF(ALLOCATED(ALOT%GW_ALLOTMENT))      DEALLOCATE(ALOT%GW_ALLOTMENT)
    IF(ALLOCATED(ALOT%GW_RATE_LIM ))      DEALLOCATE(ALOT%GW_RATE_LIM )
    IF(ALLOCATED(ALOT%SW_ALLOTMENT))      DEALLOCATE(ALOT%SW_ALLOTMENT)
    IF(ALLOCATED(ALOT%SW_RATE_LIM ))      DEALLOCATE(ALOT%SW_RATE_LIM )
  END SUBROUTINE
  !  
  SUBROUTINE INITIALIZE_ALLOTMENT_DATA( BL, ALOT, LINE, FDIM )
    CLASS(GENERIC_BLOCK_READER), INTENT(INOUT):: BL   !DATA BLOCK
    CLASS(ALLOTMENT_DATA),       INTENT(INOUT):: ALOT
    CHARACTER(*),                INTENT(INOUT):: LINE
    TYPE(FMP_DIMENSION),         INTENT(IN   ):: FDIM
    CHARACTER(5):: ERROR
    CHARACTER(6):: BYFARM
    LOGICAL:: EOF
    INTEGER:: LLOC, ISTART, ISTOP
    TYPE(WARNING_TYPE):: WARN_MSG
    !
    CALL WARN_MSG%INIT()
    !
    WRITE(BL%IOUT,'(/A/)') 'ALLOTMENT BLOCK FOUND AND NOW LOADING PROPERTIES'
    !
    ALOT%IOUT = BL%IOUT
    ALOT%LOUT = BL%IOUT
    ALOT%NFARM = FDIM%NFARM
    BYFARM = 'BYWBS'
    !
    ERROR='ERROR'
    !
    CALL BL%MAKE_SCRATCH_FILE()
    !
    !READ(BL%SCRATCH, '(A)', IOSTAT=IERR) LINE
    CALL BL%READ_SCRATCH(EOF, LINE)
    !
    DO WHILE (.NOT. EOF)
      !
      LLOC=ONE
      CALL PARSE_WORD_UP(LINE,LLOC,ISTART,ISTOP)
      !
      SELECT CASE ( LINE(ISTART:ISTOP) )
      CASE ("SURFACE_WATER", "SURFACE")
                        IF(LINE(ISTART:ISTOP) == "SURFACE") CALL PARSE_WORD(LINE,LLOC,ISTART,ISTOP) !MOVE PAST "WATER"
                        !
                        CALL PARSE_WORD_UP(LINE,LLOC,ISTART,ISTOP)
                        SELECT CASE ( LINE(ISTART:ISTOP) )
                        CASE ("RATE"  ); ALOT%SW_ALLOTMENT_TYPE = Z
                        CASE ("VOLUME"); ALOT%SW_ALLOTMENT_TYPE = ONE
                        CASE ("HEIGHT"); ALOT%SW_ALLOTMENT_TYPE = TWO
                        CASE DEFAULT;    CALL STOP_ERROR(LINE=LINE, INFILE=BL%IU, OUTPUT=BL%IOUT,MSG='FMP ALLOTMENT BLOCK ERROR. FOUND KETYWORD "SURFACE_WATER" (OR "SURFACE WATER")'//NL//'WHICH MUST BE FOLLOWED BY EITHER "HEIGHT", "VOLUME", OR "RATE".'//NL//'CHECK BLOCK FOR COMPLETENESS (viz. "SURFACE_WATER HEIGHT" or "SURFACE_WATER VOLUME" or "SURFACE_WATER RATE")')
                        END SELECT
                        !
                        WRITE(BL%IOUT,'(3A)') '   SURFACE_WATER ',LINE(ISTART:ISTOP),' KEYWORD FOUND. NOW LOADING STATIC/TRANSIENT KEYWORD AND LIST INPUT OF SURFACE WATER ALLOTMENTS BY WBS.'
                        !
                        ALOT%HAS_SW_ALLOTMENT = TRUE
                        !
                        CALL ALOT%SW_ALLOTMENT_TFR%INIT('SW_ALOT',  LLOC, LINE, BL%IOUT, BL%IU, FDIM%NFARM, ONE, Z, Z, FDIM%NFARM, BYFARM, SCRATCH=BL%SCRATCH)
                        !
                        ALLOCATE(ALOT%SW_ALLOTMENT(FDIM%NFARM))
                        ALLOCATE(ALOT%SW_RATE_LIM (FDIM%NFARM))
      CASE ("GROUNDWATER")
                        !
                        CALL PARSE_WORD_UP(LINE,LLOC,ISTART,ISTOP)
                        SELECT CASE ( LINE(ISTART:ISTOP) )
                        CASE ("RATE"  ); ALOT%GW_ALLOTMENT_TYPE = Z
                        CASE ("VOLUME"); ALOT%GW_ALLOTMENT_TYPE = ONE
                        CASE ("HEIGHT"); ALOT%GW_ALLOTMENT_TYPE = TWO
                        CASE DEFAULT;    CALL STOP_ERROR(LINE=LINE, INFILE=BL%IU, OUTPUT=BL%IOUT,MSG='FMP ALLOTMENT BLOCK ERROR. FOUND KETYWORD "GROUNDWATER"'//NL//'WHICH MUST BE FOLLOWED BY EITHER "VOLUME" OR "RATE".'//NL//'CHECK BLOCK FOR COMPLETENESS (viz. "GROUNDWATER VOLUME" or "GROUNDWATER RATE")')
                        END SELECT
                        !
                        WRITE(BL%IOUT,'(3A)') '   GROUNDWATER ',LINE(ISTART:ISTOP),' KEYWORD FOUND. NOW LOADING STATIC/TRANSIENT KEYWORD AND LIST INPUT OF SURFACE WATER ALLOTMENTS BY WBS.'
                        !
                        ALOT%HAS_GW_ALLOTMENT = TRUE
                        !
                        CALL ALOT%GW_ALLOTMENT_TFR%INIT('GW_ALOT',  LLOC, LINE, BL%IOUT, BL%IU, FDIM%NFARM, ONE, Z, Z, FDIM%NFARM, BYFARM, SCRATCH=BL%SCRATCH)
                        !
                        ALLOCATE(ALOT%GW_ALLOTMENT(FDIM%NFARM))
                        ALLOCATE(ALOT%GW_RATE_LIM (FDIM%NFARM))
      CASE DEFAULT
                        CALL WARN_MSG%ADD('FOUND UNKNOWN KEYWORD "'//LINE(ISTART:ISTOP)//'" ***IT WILL BE IGNORED***'//BLN)
      END SELECT
      !
      !READ(BL%SCRATCH, '(A)', IOSTAT=IERR) LINE
      CALL BL%READ_SCRATCH(EOF, LINE)
      !
    END DO
    !
    CALL WARN_MSG%CHECK(HED='FMP ALLOTMENT BLOCK'//NL,INFILE=BL%IU,OUTPUT=BL%IOUT,INLINE=TRUE,CMD_PRINT=TRUE,TAIL=NL)
    !
  END SUBROUTINE 
  !
  SUBROUTINE SETUP_NEXT_STRESS_PERIOD(ALOT, IRR_AREA, PERTIM)
    !
    CLASS(ALLOTMENT_DATA),                      INTENT(INOUT):: ALOT
    DOUBLE PRECISION, DIMENSION(:), CONTIGUOUS, INTENT(IN   ):: IRR_AREA!(NFARM)
    DOUBLE PRECISION,                           INTENT(IN   ):: PERTIM
    INTEGER::I
    LOGICAL:: UPDATE
    !
    IF(ALOT%TFR_READ) THEN
        UPDATE = FALSE
        CALL ALOT%SW_ALLOTMENT_TFR%NEXT()
        CALL ALOT%GW_ALLOTMENT_TFR%NEXT()
    ELSE
        UPDATE = TRUE
        ALOT%TFR_READ = TRUE
    END IF
    !
    IF(ALOT%HAS_SW_ALLOTMENT) THEN
          !
          ALOT%SW_ALLOTMENT = ALOT%SW_ALLOTMENT_TFR%LIST
          !
          IF(ALOT%SW_ALLOTMENT_TFR%SFAC%HAS_ALL) ALOT%SW_ALLOTMENT = ALOT%SW_ALLOTMENT * ALOT%SW_ALLOTMENT_TFR%SFAC%ALL
          IF(ALOT%SW_ALLOTMENT_TFR%SFAC%HAS_EX1) ALOT%SW_ALLOTMENT = ALOT%SW_ALLOTMENT * ALOT%SW_ALLOTMENT_TFR%SFAC%EX1
          !
          IF(ALOT%SW_ALLOTMENT_TYPE == TWO) ALOT%SW_ALLOTMENT = ALOT%SW_ALLOTMENT * IRR_AREA
          !
          DO CONCURRENT (I=ONE:ALOT%NFARM, ALOT%SW_ALLOTMENT(I) < DZ) 
                                                                  ALOT%SW_ALLOTMENT(I) = INF
          END DO
          !
          DO CONCURRENT (I=ONE:ALOT%NFARM)
              IF(ALOT%SW_ALLOTMENT(I) > D100) THEN
                  ALOT%SW_RATE_LIM (I) = INF
                  !
              ELSEIF(ALOT%SW_ALLOTMENT_TYPE > Z) THEN  !By SP
                  !
                  ALOT%SW_RATE_LIM (I) = ALOT%SW_ALLOTMENT(I) / PERTIM
              ELSE
                 ALOT%SW_RATE_LIM (I) = ALOT%SW_ALLOTMENT(I)
                 ALOT%SW_ALLOTMENT(I) = ALOT%SW_ALLOTMENT(I) * PERTIM
              END IF
          END DO
    END IF
    !
    IF(ALOT%HAS_GW_ALLOTMENT) THEN
          !
          ALOT%GW_ALLOTMENT = ALOT%GW_ALLOTMENT_TFR%LIST
          !
          IF(ALOT%GW_ALLOTMENT_TFR%SFAC%HAS_ALL) ALOT%GW_ALLOTMENT = ALOT%GW_ALLOTMENT * ALOT%GW_ALLOTMENT_TFR%SFAC%ALL
          IF(ALOT%GW_ALLOTMENT_TFR%SFAC%HAS_EX1) ALOT%GW_ALLOTMENT = ALOT%GW_ALLOTMENT * ALOT%GW_ALLOTMENT_TFR%SFAC%EX1
          !
          IF(ALOT%GW_ALLOTMENT_TYPE == TWO) ALOT%GW_ALLOTMENT = ALOT%GW_ALLOTMENT * IRR_AREA
          !
          DO CONCURRENT (I=ONE:ALOT%NFARM)
              IF(ALOT%GW_ALLOTMENT(I)  < DZ) THEN
                  ALOT%GW_ALLOTMENT(I) = DNEG !INF  --NOTE THAT FWEL ROUTINES SKIP ALLOTMENTS WHEN <0
                  ALOT%GW_RATE_LIM (I) = DNEG
                  !
              ELSEIF(ALOT%GW_ALLOTMENT_TYPE > Z) THEN  !By SP
                  !
                  ALOT%GW_RATE_LIM (I) = ALOT%GW_ALLOTMENT(I) / PERTIM
              ELSE
                 ALOT%GW_RATE_LIM (I) = ALOT%GW_ALLOTMENT(I)
                 ALOT%GW_ALLOTMENT(I) = ALOT%GW_ALLOTMENT(I) * PERTIM
              END IF
          END DO
    END IF
    !
  END SUBROUTINE
END MODULE
!
!#########################################################################################################
!
